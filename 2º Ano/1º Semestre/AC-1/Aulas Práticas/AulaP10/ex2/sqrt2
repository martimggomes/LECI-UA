	.data
k0:	.double 1.0
k1:	.double 0.5
k2:	.double 0.0
	.text
	.globl sqrt2
	
# Mapa de registos:
# val: $f12
# aux: $f2
# xn: $f4
# i: $t0

sqrt2:
	la $t0,k0			# $t0 = &k0 
	l.d $f2,0($t0)			# aux = 1.0
	l.d $f4,0($t0)			# xn = 1.0
	la $t0,k1
	l.d $f6,0($t0)			# $f6 = 0.5
	la $t0,k2
	l.d $f8,0($t0)			# $f8 = 0.0
	li $t0,0			# i = 0

if:	
	c.le.d $f12,$f8			# if(val > 0.0)		
	bc1f else
	
do:
	mov.d $f2,$f4			# aux = xn
	div.d $f4,$f12,$f2		# xn = val/xn
	add.d $f4,$f4,$f2		# xn = xn + val/xn
	mul.d $f4,$f6,$f4		# xn = 0.5 * (xn + val/xn)
	
while:
	c.eq.d $f2,$f4			# while((aux != xn))
	bc1t endif
	
	addi $t0,$t0,1
	blt $t0,25,do			# while (++i < 25)
	j endif
	
else:
	mov.d $f4,$f8			# xn = 0.0

endif:
	mov.d $f0,$f4			# return xn
	jr $ra

	

